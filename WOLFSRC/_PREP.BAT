@ECHO OFF

REM Don't allow accidental execution by the user
IF "%7"=="" GOTO :EXIT

REM Preserve the current version's .EXE
CALL VERSION.BAT

REM Clean up
IF EXIST OBJ\*.OBJ DEL OBJ\*.OBJ
IF EXIST OBJ\WOLF3D.EXE DEL OBJ\WOLF3D.EXE
IF EXIST OBJ\WOLF3D.MAP DEL OBJ\WOLF3D.MAP
IF EXIST WOLF3D.SYM DEL WOLF3D.SYM
COPY OBJ.%1\*.* OBJ

REM Setup VERSION.H
COPY %2 VERSION.H
IF "%8"=="" ECHO #define WOLFDOSMPU >> VERSION.H

REM Setup VERSION.EQU
CD . > VERSION.EQU
IF "%8"=="" ECHO WOLFDOSMPU	equ	1 >> VERSION.EQU

REM Setup VERSION.BAT
ECHO @ECHO OFF > VERSION.BAT
ECHO IF NOT EXIST OBJ\WOLF3D.EXE GOTO :DONE >> VERSION.BAT
ECHO LZEXE OBJ\WOLF3D.EXE >> VERSION.BAT
IF "%8"=="" ECHO IF EXIST WOLF3D.EXE COPY WOLF3D.EXE %3.EXE >> VERSION.BAT
IF "%8"=="" ECHO IF EXIST OBJ\WOLF3D.MAP COPY OBJ\WOLF3D.MAP %3.MAP >> VERSION.BAT
IF NOT "%8"=="" ECHO IF EXIST WOLF3D.EXE COPY WOLF3D.EXE %4.EXE >> VERSION.BAT
IF NOT "%8"=="" ECHO IF EXIST OBJ\WOLF3D.MAP COPY OBJ\WOLF3D.MAP %4.MAP >> VERSION.BAT
ECHO IF EXIST OBJ\WOLF3D.OLD REN OBJ\WOLF3D.OLD WOLF3D.EXE >> VERSION.BAT
ECHO IF EXIST LZEXE.TMP DEL LZEXE.TMP >> VERSION.BAT
ECHO IF EXIST WOLF3D.EXE DEL WOLF3D.EXE >> VERSION.BAT
ECHO :DONE >> VERSION.BAT

REM Done
ECHO ================
ECHO Game is now %5 %6 %7
IF "%8"=="" ECHO.	MPU enabled
IF NOT "%8"=="" ECHO.	MPU disabled

:EXIT
