@ECHO OFF

REM Don't allow accidental execution by the user
IF "%6"=="" GOTO :EXIT

SET WOLFDOSMPU=%7
IF "%WOLFDOSMPU%"=="m" SET WOLFDOSMPU=M
IF "%WOLFDOSMPU%"=="v" SET WOLFDOSMPU=V
IF "%WOLFDOSMPU%"=="M" GOTO :CONTINUE
IF "%WOLFDOSMPU%"=="V" GOTO :CONTINUE
SET WOLFDOSMPU=W

:CONTINUE
REM Preserve the current version's .EXE
CALL VERSION.BAT

REM Clean up
IF EXIST OBJ\*.OBJ DEL OBJ\*.OBJ
IF EXIST OBJ\WOLF3D.EXE DEL OBJ\WOLF3D.EXE
IF EXIST OBJ\WOLF3D.MAP DEL OBJ\WOLF3D.MAP
IF EXIST WOLF3D.SYM DEL WOLF3D.SYM
COPY OBJ.%1\*.* OBJ

REM Setup VERSION.H
COPY %2 VERSION.H
TYPE _WVER.H >> VERSION.H
IF "%WOLFDOSMPU%"=="W"	ECHO #define WOLFDOSMPU >> VERSION.H
IF "%WOLFDOSMPU%"=="W"	ECHO #define WASD >> VERSION.H
IF "%WOLFDOSMPU%"=="M"	ECHO #define WOLFDOSMPU >> VERSION.H

REM Setup VERSION.EQU
CD . > VERSION.EQU
IF "%WOLFDOSMPU%"=="W"	ECHO WOLFDOSMPU	equ	1 >> VERSION.EQU
IF "%WOLFDOSMPU%"=="M"	ECHO WOLFDOSMPU	equ	1 >> VERSION.EQU

REM Setup VERSION.BAT
ECHO @ECHO OFF > VERSION.BAT
ECHO IF NOT EXIST OBJ\WOLF3D.EXE GOTO :DONE >> VERSION.BAT
ECHO LZEXE OBJ\WOLF3D.EXE >> VERSION.BAT
ECHO IF EXIST WOLF3D.EXE COPY WOLF3D.EXE %3%WOLFDOSMPU%.EXE >> VERSION.BAT
ECHO IF EXIST OBJ\WOLF3D.MAP COPY OBJ\WOLF3D.MAP %3%WOLFDOSMPU%.MAP >> VERSION.BAT
ECHO IF EXIST OBJ\WOLF3D.OLD REN OBJ\WOLF3D.OLD WOLF3D.EXE >> VERSION.BAT
ECHO IF EXIST LZEXE.TMP DEL LZEXE.TMP >> VERSION.BAT
ECHO IF EXIST WOLF3D.EXE DEL WOLF3D.EXE >> VERSION.BAT
ECHO :DONE >> VERSION.BAT

REM Done
ECHO ================
IF "%7"=="_" GOTO :EXIT
ECHO Game is now %4 %5 %6
IF "%WOLFDOSMPU%"=="W"	ECHO.	MPU support + WASD controls
IF "%WOLFDOSMPU%"=="M"	ECHO.	MPU support only
IF "%WOLFDOSMPU%"=="V"	ECHO.	Vanilla id source release

:EXIT
